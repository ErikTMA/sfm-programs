-- This is for everything Mekanism processing.
-- For the infusers, it assumes the west side is the extra input. If it's not, you'll need to change the side.

name "Mekanism Processing"

-- Infusers
every 20 ticks do
    input from Infusion_Extra_Input
    output enriched_diamond, diamond_dust to each Diamond_Infusers west side
    output enriched_carbon, charcoal_block, coal, dust_coal, charcoal, dust_charcoal to each Carbon_Infusers west side
    output enriched_redstone, redstone_block, "redstone" to each Redstone_Infusers west side
    output enriched_refined_obsidian, dust_refined_obsidian to each Refined_Obsidian_Infusers west side
    output enriched_gold, dust_gold to each Gold_Infusers west side
    output enriched_tin, tin_dust to each Tin_Infusers west side
forget
    input from Infusion_Input
    output alloy_infused, dust_obsidian to each Diamond_Infusers top side
    output iron_block_1x, iron_dust, gold_dust, enriched_iron to each Carbon_Infusers top side
    output iron_ingot, quartz, osmium_ingot, copper_ingot to each Redstone_Infusers top side
    output alloy_reinforced to each Refined_Obsidian_Infusers top side
    output netherite_scrap, blackstone to each Gold_Infusers top side
    output copper_dust, copper_ingot to each Tin_Infusers top side
end

-- Enrichers
every 20 ticks do
    input from Enrichers_Input round robin by block
    output 27 to each Enrichers top side
end

-- Crushers
every 20 ticks do
    input from Crushers_Input round robin by block
    output 27 to each Crushers top side
end

-- Compressors
every 20 ticks do
    input from Infusion_Extra_Input
    output osmium_ingot to each Compressors west side
forget
    input from Compressor_Input
    output to each Compressors top side
end

-- Purification
every 20 ticks do
    input from Purification_Input
    output 27 to each Purifiers top side
end

-- Injectors
every 20 ticks do
    input from Injector_Input
    output 27 to each Injectors top side
end

-- Oxygen
every 20 ticks do
    input gas:: from Oxygen_Input top side
    output gas:: to each Purifiers west side
end

-- Hydrogen Chloride
every 20 ticks do
    input gas:: from each Hydrogen_Chloride_Input top side
    output gas:: to each Injectors west side
forget
    input from each Hydrogen_Chloride_Input
    output to each Injectors west side
end






-- Collect
every 20 ticks do
    input from each Enrichers, Crushers, Diamond_Infusers, Carbon_Infusers, Redstone_Infusers, Refined_Obsidian_Infusers, Gold_Infusers, Tin_Infusers, Compressors, Purifiers, Injectors bottom side
    output to "Output"
end


every 20 ticks do
    input from each Enrichers, Crushers, Diamond_Infusers, Carbon_Infusers, Redstone_Infusers, Refined_Obsidian_Infusers, Gold_Infusers, Tin_Infusers, Compressors, Purifiers, Injectors bottom side
    output to "Output"
end


every 20 ticks do
    input from each Enrichers, Crushers, Diamond_Infusers, Carbon_Infusers, Redstone_Infusers, Refined_Obsidian_Infusers, Gold_Infusers, Tin_Infusers, Compressors, Purifiers, Injectors bottom side
    output to "Output"
end

-- Power
every 1 ticks do
    input fe:: from Power_Input bottom side
    output fe:: to each Diamond_Infusers, Carbon_Infusers, Redstone_Infusers, Refined_Obsidian_Infusers, Gold_Infusers, Tin_Infusers, Enrichers, Crushers, Compressors, Purifiers, Injectors bottom side
end
